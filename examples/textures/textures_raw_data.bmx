SuperStrict

Framework Ray.Lib

' Initialization
'--------------------------------------------------------------------------------------
Const screenWidth:Int = 800
Const screenHeight:Int = 450

InitWindow(screenWidth, screenHeight, "raylib [textures] example - texture from raw data")

' NOTE: Textures MUST be loaded after Window initialization (OpenGL context is required)

' Load RAW image data (512x512, 32bit RGBA, no file header)
Local fudesumiRaw:RImage = LoadImageRaw("../../lib.mod/raylib/examples/textures/resources/fudesumi.raw", 384, 512, UNCOMPRESSED_R8G8B8A8, 0)
Local fudesumi:RTexture2D = LoadTextureFromImage(fudesumiRaw)  ' Upload CPU (RAM) image to GPU (VRAM)
UnloadImage(fudesumiRaw)                                ' Unload CPU (RAM) image data

' Generate a checked texture by code
Local width:Int = 960
Local height:Int = 480

' Dynamic memory allocation to store pixels data (Color type)
Local pixels:RColor[width*height]

For Local y:Int = 0 Until height
	For Local x:Int = 0 Until width
		If ((x/32+y/32)/1) Mod 2 = 0 Then
			pixels[y*width + x] = ORANGE
		Else
			pixels[y*width + x] = GOLD
		End If
	Next
Next

' Load pixels data into an image structure and create texture
Local checkedIm:RImage = LoadImageEx(pixels, width, height)
Local checked:RTexture2D = LoadTextureFromImage(checkedIm)
UnloadImage(checkedIm)         ' Unload CPU (RAM) image data


'---------------------------------------------------------------------------------------

' Main game loop
While Not WindowShouldClose()    ' Detect window close button or ESC key
	' Update
	'----------------------------------------------------------------------------------
	' TODO: Update your variables here
	'----------------------------------------------------------------------------------

	' Draw
	'----------------------------------------------------------------------------------
	BeginDrawing()

		ClearBackground(RAYWHITE)

		DrawTexture(checked, screenWidth/2 - checked.width/2, screenHeight/2 - checked.height/2, Fade(WHITE, 0.5))
		DrawTexture(fudesumi, 430, -30, WHITE)

		DrawText("CHECKED TEXTURE ", 84, 85, 30, BROWN)
		DrawText("GENERATED by CODE", 72, 148, 30, BROWN)
		DrawText("and RAW IMAGE LOADING", 46, 210, 30, BROWN)

		DrawText("(c) Fudesumi sprite by Eiden Marsal", 310, screenHeight - 20, 10, BROWN)

	EndDrawing()
	'----------------------------------------------------------------------------------
Wend

' De-Initialization
'--------------------------------------------------------------------------------------
UnloadTexture(fudesumi)    ' Texture unloading
UnloadTexture(checked)     ' Texture unloading

CloseWindow()              ' Close window and OpenGL context
'--------------------------------------------------------------------------------------
